DEFINE_ACTION_FUNCTION cd_arcane_post BEGIN

  INCLUDE "%MOD_FOLDER%/%component_loc%/lib/cdfunctions.tpa"
  
  ACTION_IF enhanced_edition BEGIN
    
    COPY ~%MOD_FOLDER%/%component_loc%/resource/0105.ini~    ~override~ // lance of disruption animation
         ~%MOD_FOLDER%/%component_loc%/resource/lodisru.bam~ ~override~
    
    APPEND ~animate.ids~ ~0x0105 LANCE_OF_DISRUPTION~   UNLESS ~^0x0105~ 
    
  END  
       
  COPY_EXISTING ~spwi114.spl~ ~override~ // update shield to block MFM, too
    LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = ~spwi112~ resource = EVAL ~%WIZARD_MORDENKAINENS_FORCE_MISSILES%~ END
    LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = ~spwi112~ resource = EVAL ~%WIZARD_MORDENKAINENS_FORCE_MISSILES%b~ END 
    
  // update wand of monster summoning to use new tables
  // first, find table value
  COPY_EXISTING ~%WIZARD_MONSTER_SUMMONING_2%.spl~ ~override~
    READ_LONG 0x6a fx_off
    SET total_fx = ((SOURCE_SIZE - fx_off) / 0x30)
    FOR (index = 0 ; index < total_fx ; ++index) BEGIN
      READ_SHORT (fx_off +        (index * 0x30)) opcode
      PATCH_IF opcode = 331 BEGIN
        READ_LONG (fx_off + 0x08 + (index * 0x30)) ms2table
        SET index = total_fx // kill loop
      END
    END
    BUT_ONLY
    
  COPY_EXISTING ~wand10.itm~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 127 opcode = 331 parameter1 = 12 parameter2 = ms2table END // six creatures

  // clone IWD references into BG2; remove all bg2 shapeshifting references
  COPY_EXISTING ~%WIZARD_ANTIMAGIC_SHELL%.spl~ ~override~
    LPF CLONE_EFFECT INT_VAR match_opcode = 123 STR_VAR match_resource = goodber resource = gberry END // good berries
    LPF CLONE_EFFECT INT_VAR match_opcode = 123 STR_VAR match_resource = fseeds  resource = fireseed END // fire seeds
    PATCH_FOR_EACH file IN brblp wolfm brbrp plyjelly plyspid plyfist plymstar mindflay goliro trollall wolfgr firern earthrn 
    chillt cdia480 cdia481 cdia482 cdidree cdidrfe cdidrwe cdidrbr BEGIN // chill touch, new polys 
      LPF CLONE_EFFECT INT_VAR match_opcode = 112 STR_VAR match_resource = smcudge resource = EVAL ~%file%~ END // clone from star-metal cudgel
    END
    
  // shadow trolls need to lose death-and-revive script. regen item also provides EE death-and-revive sequence; kill item and apply regen directly to cre  
  COPY_EXISTING ~SS3trl7.cre~ ~override~ // troll
                ~SS3trl8.cre~ ~override~ // troll
    WRITE_ASCII 0x258 ~~ #8 // nuke eftrolg reference, even though script is blank
    LPF DELETE_CRE_ITEM STR_VAR item_to_delete = reg1hp2 END // in helm slot
    LPF ADD_CRE_EFFECT INT_VAR opcode = 98 parameter1 = 2 parameter2 = 3 timing = 1 END
    
  // Iron Body: Spell should protect against a wider range of petrification i.e. Basilisk Stare BASIGAZE.itm 
  // delete three specific protections against flesh-to-stone spells with general petrification immunity  
  COPY_EXISTING ~%WIZARD_IRON_BODY%.spl~ ~override~
    LPF DELETE_EFFECT INT_VAR match_opcode = 318 STR_VAR match_resource = spwi604 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 318 opcode = 101 parameter2 =   134 STR_VAR match_resource = spwi053 END // immunity: petrification
    LPF ALTER_EFFECT INT_VAR match_opcode = 318 opcode = 267 parameter1 = 14665 STR_VAR match_resource = spin984 END // prevent "petrified" string
    
  // Mordenkainen's Force Blade: Why not use identical visuals to the L2 Flame Blade/L7 Mordy's Sword? 
  COPY_EXISTING ~%WIZARD_MORDENKAINENS_SWORD_IWD%.spl~ ~override~
    LPF DELETE_EFFECT INT_VAR match_opcode = 61 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 141 opcode = 215 parameter2 = 0 STR_VAR resource = spflmbld END // use flaming sword graphic
 
  ACTION_IF enhanced_edition BEGIN
  
    // Eye of Fortitude should target an area. 
    COPY_EXISTING ~%SEVEN_EYES_FORTITUDE%.spl~ ~override~
      LPF ALTER_EFFECT INT_VAR match_opcode = 146 opcode = 148 target = 1 END
      
    // Seven Eyes: Spell resnames in the 7EYES.2da need to be keyed to all the different clones/variations of BG1/2.
    // note: i didn't even bother checking bgee/sod resrefs since you realistically won't see this spell until bg2
    COPY_EXISTING ~7eyes.2da~ ~override~
      // non-iwd slay living requires a touch attack with this weapon, not an instant magic attack, e.g. sppr511 > slaylive
      // arcane/divine symbols death, npc pw:kill, banshee wail
      REPLACE_TEXTUALLY ~sppr511\([ %TAB%]+sppr7[0-4][0-9]\).+$~ ~slaylive\1 sppr719 spwi817 spwi897 spin681~ 
    
    // continuing the above
    OUTER_SET cd_columns = 9    
    ACTION_FOR_EACH spell IN spin991 sppr599 sppr708 spwi913 BEGIN /// beholder death ray, npc slay living, finger of death, wail of the banshee

      OUTER_SET cd_columns += 1
      APPEND_COL ~7eyes.2da~ ~$ $ %cd_columns% * * * * %spell% * *~
    
    END   
  
    // don't have IWD's random treasure table to assign weapon
    COPY_EXISTING ~ad3sklm.cre~ ~override~ // soul eater skeleton
      LPF ALTER_CREATURE_ITEM INT_VAR flag_unstealable = 1 STR_VAR match_item = rndtre40 item = hamm01 END 
      WRITE_ASCII  0x248 ~cd3sklm~   #8 // override script; randomizes weapon
      WRITE_ASCIIE 0x268 ~%default%~ #8 // default script
    
    COMPILE ~%MOD_FOLDER%/%component_loc%/resource/cd3sklm.baf~  
    
    ACTION_IF ((MOD_IS_INSTALLED ~iwdification/setup-iwdification.tp2~ ~120~) AND (FILE_EXISTS ~iwdification/evasion/evasion_arcane.2da~)) BEGIN

      INCLUDE ~iwdification/evasion/evasion.tpa~
      LAF cd_add_evasion STR_VAR 2da_file = ~iwdification/evasion/evasion_arcane.2da~ END 
    
    END 
    
  END
  
  /////                                                  \\\\\
  ///// cosmetic suggestions - thanks, Galactygon!       \\\\\
  /////                                                  \\\\\
  
  // Decastave: I'd remove the glow and the SHAIR lighting effects from the base spell. 
  COPY_EXISTING ~%WIZARD_DECASTAVE%.spl~ ~override~
    LPF DELETE_EFFECT INT_VAR match_opcode = 141 END
  
  // DECASTA.itm needs to be updated to use red SHEARTH and glow. 
  COPY_EXISTING ~decasta.itm~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 8 parameter1 = ~0x00006400~ END // from 0/100/0 to 100/0/0
    LPF ALTER_EFFECT INT_VAR match_opcode = 215 opcode = 141 parameter2 = 1 END // from invoch

 // Emotion spells: the projectiles should appear more similar to the small area of effect sparkle area gold projectiles (i.e. HOLD.pro).
 COPY_EXISTING ~idpro255.pro~ ~override~
               ~idpro407.pro~ ~override~
    WRITE_LONG  0x0c (THIS | BIT0) // add 'show sparks' flag
    WRITE_SHORT 0x28 4 // gold   

  // Darts of Bone: DOBONE.itm uses IWD style visuals. Should be using necromancy/aqua SHEARTH plus accompanying glow and sound 
  COPY_EXISTING ~dobone.itm~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 215 opcode = 141 parameter2 = 1 END // from necroh
    LPF ALTER_EFFECT INT_VAR match_opcode =  61 parameter1 = ~0x785a3c00~  END // 60/90/120

  // Lich Touch: LTOUCH.spl should be using necromancy/aqua SHEARTH plus accompanying glow and sound. Currently using IWD-style visuals. 
  COPY_EXISTING ~ltouch.spl~ ~override~
    LPF ALTER_EFFECT INT_VAR match_opcode = 215 opcode = 141 parameter2 = 1 END // from necroh
    
END