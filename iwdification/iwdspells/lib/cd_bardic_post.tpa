DEFINE_ACTION_FUNCTION cd_bardic_post BEGIN

  ACTION_IF VARIABLE_IS_SET $IWD_spell_installed("BARD_SONG_KAUDIES") BEGIN

    // Kaudies protects from some BG-specific sound effects
    COPY_EXISTING ~%BARD_SONG_KAUDIES%.spl~  ~override~
      LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spwi431 resource = spin789 END // shout > demilich howl
      LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spwi431 resource = spin891 END // shout > moon dog howl
      LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = spwi431 resource = spcl908 END // shout > war cry
  
  END
  
  // compatibility with Garrick: Tales of a Troubadour
  ACTION_IF FILE_EXISTS_IN_GAME ~gtt#sno.spl~ BEGIN     //Compatibility with garrick-tt begins here
  
    OUTER_SET code=IDS_OF_SYMBOL ("spell" "BARD_SONG_BALLAD")
    OUTER_SET code=code - 4000 
    COPY_EXISTING ~gtt#sno.spl~ ~override~    
      LPF ALTER_EFFECT INT_VAR match_opcode = 251 STR_VAR resource = EVAL ~spcl%code%~ END

    COPY_EXISTING ~_garri.bcs~   ~override~ // tutu
                  ~bggarrik.bcs~ ~override~ // bgt
                  ~garrick.bcs~  ~override~ // bgee 
                  ~garrick_.bcs~ ~override~ // eet 
      DECOMPILE_AND_PATCH BEGIN
        REPLACE_TEXTUALLY ~gtt#sno~ ~#BARD1~
      END 
      IF_EXISTS      

  END 
 
END