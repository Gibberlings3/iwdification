/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter Paladin Spell Progression Table                      \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

// maintenance note: this is crosslinked; use mod-agnostic paths
// * cdtweaks/lib/comp_2390.tpa
// * iwdification/tpa/comp_2390.tpa

/////                                                            \\\\\
///// Paladins Use HoW Spell Progression                         \\\\\
/////                                                            \\\\\

COPY ~%MOD_FOLDER%/2da/iwd_mxsplpal.2da~ ~override/mxsplpal.2da~

// paladins are hardcoded to use their level - 8 as their caster level
// since we're moving up their first spell 3 levels, we increase their casting level manually by 3
ACTION_IF FILE_EXISTS_IN_GAME ~kitlist.2da~ BEGIN

  COPY ~%MOD_FOLDER%/spl/cdtapals.spl~ ~override~

  ACTION_CLEAR_ARRAY cd_paladin_kits
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_paladin_kits BEGIN clabpa01 => 6 END

  COPY_EXISTING ~kitlist.2da~ ~override~
    COUNT_2DA_ROWS 9 rows
    FOR (index = 2 ; index < rows ; ++index) BEGIN // skip reserve row
      READ_2DA_ENTRY index 8 9 class
      PATCH_IF (class = 6) BEGIN // paladin
        READ_2DA_ENTRY index 5 9 clab
        DEFINE_ASSOCIATIVE_ARRAY cd_paladin_kits BEGIN "%clab%" => ~%class%~ END // find all druid clabs
      END  
    END
    BUT_ONLY

  ACTION_PHP_EACH cd_paladin_kits AS clab => class BEGIN

    ACTION_IF FILE_EXISTS_IN_GAME ~%clab%.2da~ BEGIN

      APPEND ~%clab%.2da~ ~ABILITYY   ****       ****       ****       ****       ****       AP_cdtapals CDREPLACE~
   
      COPY_EXISTING ~%clab%.2da~ ~override~
        COUNT_2DA_COLS cols
        FOR (index = 8 ; index < cols ; ++index) BEGIN
          REPLACE_TEXTUALLY ~CDREPLACE~ ~**** CDREPLACE~
        END
        REPLACE_TEXTUALLY ~CDREPLACE~ ~****~
        PRETTY_PRINT_2DA
        BUT_ONLY
        
    END

  END   
  
END
