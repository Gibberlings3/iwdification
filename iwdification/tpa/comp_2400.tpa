/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                            \\\\\
///// Alter Ranger Spell Progression Table                       \\\\\
/////                                                            \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

// maintenance note: this is crosslinked; use mod-agnostic paths
// * cdtweaks/lib/comp_2400.tpa
// * iwdification/tpa/comp_2400.tpa

/////                                                            \\\\\
///// Rangers Use HoW Spell Progression                          \\\\\
/////                                                            \\\\\

ACTION_IF game_is_iwd2 THEN BEGIN

  COPY ~%MOD_FOLDER%/2da/iwd_mxsplran.2da~ ~override/mxsplrgr.2da~

END ELSE BEGIN

  COPY ~%MOD_FOLDER%/2da/iwd_mxsplran.2da~ ~override/mxsplran.2da~

END

// rangers are hardcoded to use their level - 7 as their caster level
// since we're moving up their first spell 2 levels, we increase their casting level manually by 2
ACTION_IF FILE_EXISTS_IN_GAME ~kitlist.2da~ BEGIN

  COPY ~%MOD_FOLDER%/spl/cdtarans.spl~ ~override~
       ~%MOD_FOLDER%/eff/cdtarans.eff~ ~override~

  ACTION_CLEAR_ARRAY cd_ranger_kits
  ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_ranger_kits BEGIN clabrn01 => 12 END

  COPY_EXISTING ~kitlist.2da~ ~override~
    COUNT_2DA_ROWS 9 rows
    FOR (index = 2 ; index < rows ; ++index) BEGIN // skip reserve row
      READ_2DA_ENTRY index 8 9 class
      PATCH_IF (class = 12) BEGIN // ranger
        READ_2DA_ENTRY index 5 9 clab
        DEFINE_ASSOCIATIVE_ARRAY cd_ranger_kits BEGIN "%clab%" => ~%class%~ END // find all druid clabs
      END  
    END
    BUT_ONLY

  ACTION_PHP_EACH cd_ranger_kits AS clab => class BEGIN

    ACTION_IF FILE_EXISTS_IN_GAME ~%clab%.2da~ BEGIN

      APPEND ~%clab%.2da~ ~ABILITYY   ****       ****       ****       ****       ****       AP_cdtarans CDREPLACE~
   
      COPY_EXISTING ~%clab%.2da~ ~override~
        COUNT_2DA_COLS cols
        FOR (index = 8 ; index < cols ; ++index) BEGIN
          REPLACE_TEXTUALLY ~CDREPLACE~ ~**** CDREPLACE~
        END
        REPLACE_TEXTUALLY ~CDREPLACE~ ~****~
        PRETTY_PRINT_2DA
        BUT_ONLY
        
    END

  END   
  
END
