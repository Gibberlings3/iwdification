DEFINE_ACTION_FUNCTION arcane_spells_postproduction STR_VAR tra="" BEGIN

    ACTION_IF is_iwd BEGIN
           WITH_TRA "%tra%" BEGIN
              LAF force_blade END
            END
    END ELSE BEGIN
          LAF include STR_VAR file=summoned_monsters.tpa location="%iwdspells_lib%" END
          LAF level_immunity_mage END
          LAF expeditious_retreat_icon END
		  LAF monster_summoning_arcane_cosmetic END
          ACTION_IF "%tra%" STRING_EQUAL_CASE "" BEGIN
            ACTION_IF !demivrgvs BEGIN
              LAF summoned_monsters_arcane END
              LAF include STR_VAR file=elemental_summoning.tpa location="%iwdspells_lib%" END
              LAF elemental_summoning RET WIZARD_CONJURE_WATER_ELEMENTAL_SCROLL=scroll_new END
            END
            LAF force_blade END
          END ELSE BEGIN
            WITH_TRA "%tra%" BEGIN
              ACTION_IF !demivrgvs BEGIN
                LAF summoned_monsters_arcane END
                LAF include STR_VAR file=elemental_summoning.tpa location="%iwdspells_lib%" END
                LAF elemental_summoning RET WIZARD_CONJURE_WATER_ELEMENTAL_SCROLL=scroll_new END
				LAF truncate_force_missile_description END
              END
              LAF force_blade END
            END
          END
      INCLUDE ~%obg2_res_path%/functions.tpa~
      INCLUDE ~%obg2_res_path%/cd_arcane_post.tpa~
      INCLUDE ~%obg2_res_path%/cross_patch_both.tpa~
      INCLUDE ~%obg2_res_path%/cross_patch_bard.tpa~
    END

END

DEFINE_ACTION_FUNCTION force_blade BEGIN

   ACTION_IF VARIABLE_IS_SET $IWD_spell_installed("WIZARD_MORDENKAINENS_SWORD_IWD") || VARIABLE_IS_SET $IWD_spell_installed("WIZARD_MORDENKAINENS_SWORD") BEGIN
   
	// 
	// rename the spell
    COPY_EXISTING "%WIZARD_MORDENKAINENS_SWORD_IWD%.SPL" override
        READ_STRREF NAME1 old_name
        SPRINT new_name @10
        SAY NAME1 "%new_name%"
        READ_STRREF 0x50 old_desc
        INNER_PATCH_SAVE new_desc "%old_desc%" BEGIN
           REPLACE_TEXTUALLY "%old_name%" "%new_name%"
        END
        SAY 0x50 "%new_desc%"
    BUT_ONLY

    // rename the scroll
    COPY_EXISTING "%WIZARD_MORDENKAINENS_SWORD_IWD_SCROLL%.ITM" override
        SAY NAME2 "%new_name%"
        SAY DESC "%new_desc%"
    BUT_ONLY
    
    // Reverse the icons, since the green one (attached to SWORD_IWD) ought to be associated with the summons effect

    ACTION_FOR_EACH letter IN a b c BEGIN
         COPY_EXISTING "%WIZARD_MORDENKAINENS_SWORD_IWD%%letter%.bam" "%workspace%"
         COPY_EXISTING "%WIZARD_MORDENKAINENS_SWORD%%letter%.bam" "override/%WIZARD_MORDENKAINENS_SWORD_IWD%%letter%.bam"
         COPY "%workspace%/%WIZARD_MORDENKAINENS_SWORD_IWD%%letter%.bam" "override/%WIZARD_MORDENKAINENS_SWORD%%letter%.bam"
    END
    
    ACTION_IF is_iwd BEGIN
    // on IWD, manually make a scroll and add it to the store
		LAF make_scroll_of_spell
		STR_VAR resref="%WIZARD_MORDENKAINENS_SWORD%"
			name_func=cd_scroll_name
		RET scrollname
	 END	
     COPY_EXISTING edion.sto override
       ADD_STORE_ITEM "%scrollname%" AFTER "%WIZARD_MORDENKAINENS_SWORD_IWD_SCROLL%" #1 #0 #0  IDENTIFIED #1
     BUT_ONLY
    END
	
	// also on IWD, extend the level blocks for the BG2 version to 30th level
	
	MAKE_PATCH
		clone_ability_inline=>"number_to_add=>10 match=>~ability_min_level=20~ ability_min_level=>~21+entry_index~"
		patch_effect_inline=>"match=>~ability_min_level>20 and duration>0~ duration=>~ability_min_level*6~"
	END
	LAF edit_spell STR_VAR spell="%WIZARD_MORDENKAINENS_SWORD%" edits=patch_data END

  END

END

DEFINE_ACTION_FUNCTION level_immunity_mage BEGIN

   ACTION_IF VARIABLE_IS_SET $IWD_spell_installed("WIZARD_SNILLOCS_SNOWBALL_SWARM") BEGIN
    ACTION_FOR_EACH resource IN "%WIZARD_MINOR_GLOBE_OF_INVULNERABILITY%.spl" "%WIZARD_GLOBE_OF_INVULNERABILITY%.spl" "SPWM126.spl" "JWGLOBE.spl" "lich.itm" "rakring.itm" "demilich.itm" BEGIN
     ACTION_IF FILE_EXISTS_IN_GAME "%resource%" BEGIN
      COPY_EXISTING "%resource%" override
         LPF CLONE_EFFECT INT_VAR multi_match=1 match_opcode=206 STR_VAR resource="%WIZARD_SNILLOCS_SNOWBALL_SWARM%" END
      BUT_ONLY
     END
    END
   END

END


DEFINE_ACTION_FUNCTION expeditious_retreat_icon BEGIN

 ACTION_IF VARIABLE_IS_SET $IWD_spell_installed("WIZARD_EXPEDITIOUS_RETREAT") BEGIN
    COPY_EXISTING "%WIZARD_EXPEDITIOUS_RETREAT%.spl" override
       LPF ALTER_EFFECT INT_VAR match_opcode=142 parameter2=195 END
 END

END

DEFINE_ACTION_FUNCTION monster_summoning_arcane_cosmetic BEGIN

	ACTION_FOR_EACH 2da IN msummo1 msummo2 msummo3 msummo4 msummo5 msummo6 msummo7 BEGIN
		ACTION_IF FILE_EXISTS_IN_GAME "%2da%.2da" BEGIN
			COPY_EXISTING "%2da%.2da" override
				REPLACE_TEXTUALLY "msumm1h.*" "spmonsum none"
			BUT_ONLY
		END
	END
	ACTION_IF FILE_EXISTS_IN_GAME "sshadow.2da" BEGIN
		COPY_EXISTING "sshadow.2da" override
			REPLACE_TEXTUALLY "msumm1h.*" "spraised none"
		BUT_ONLY
	END	
	ACTION_FOR_EACH 2da IN caelemw cfelemw ceelemw cwelemw BEGIN
		ACTION_IF FILE_EXISTS_IN_GAME "%2da%.2da" BEGIN
			COPY_EXISTING "%2da%.2da" override
				REPLACE_TEXTUALLY "msumm1h.*" "spportal none"
			BUT_ONLY
		END
	END



END

DEFINE_ACTION_FUNCTION truncate_force_missile_description BEGIN

 ACTION_IF VARIABLE_IS_SET $IWD_spell_installed("WIZARD_MORDENKAINENS_FORCE_MISSILES") BEGIN
		COPY_EXISTING "%WIZARD_MORDENKAINENS_FORCE_MISSILES%.spl" override
			READ_LONG 0x50 desc_strref
			GET_STRREF desc_strref desc
			SPRINT old @18
			SPRINT new @19
			INNER_PATCH_SAVE desc "%desc%" BEGIN
				REPLACE_TEXTUALLY "%old%" "%new%"
			END
		BUT_ONLY
		STRING_SET_EVALUATE desc_strref "%desc%"
 END



END