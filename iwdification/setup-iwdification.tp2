BACKUP ~weidu_external/iwdification/backup~ // location to store files for uninstall purposes
SUPPORT ~https://www.gibberlings3.net/forums/forum/185-iwdification/~ 

VERSION ~v8~

AUTO_EVAL_STRINGS

README ~iwdification/readme-iwdification-%LANGUAGE%.html~ ~iwdification/readme-iwdification.html~

ALWAYS

  ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN FAIL @999 END // DLC Merger check

  ACTION_IF !VARIABLE_IS_SET enhanced_edition THEN BEGIN

    // usual SFO loading - usually done in ALWAYS
    OUTER_SPRINT sfo_library_path "sfo" // the path (relative to your mod directory) to the directory SFO is in. Usually 'sfo'.
    OUTER_SPRINT sfo_tra_path "languages" // the path (relative to your mod directory) to the directory in which you keep tra files. Usually 'lang' or 'tra'.
    INCLUDE "%MOD_FOLDER%/%sfo_library_path%/sfo_init.tpc"
    
    INCLUDE ~iwdification/tpa/g3_cpmvars_master.tpa~ // cpm variables
  
    ACTION_IF !GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ THEN BEGIN

      OUTER_SET enhanced_edition = 1
      LOAD_TRA ~iwdification/languages/%LANGUAGE%/game_strings_ee.tra~
      OUTER_SPRINT ~tra_location~ ~iwdification/languages~
    
    END ELSE BEGIN
  
      OUTER_SET enhanced_edition = 0

      // convert strings from UTF-8 for originals
      ACTION_DEFINE_ARRAY cdreload BEGIN game_strings END
      ACTION_DEFINE_ARRAY cdnoconvert BEGIN game_strings_ee setup END
      LAF HANDLE_CHARSETS INT_VAR from_utf8 = 1 infer_charsets = 1 
                          STR_VAR default_language = ~english~ tra_path = ~iwdification/languages~ out_path = ~weidu_external/iwdification/languages~ noconvert_array = cdnoconvert reload_array = cdreload END
      OUTER_SPRINT ~tra_location~ ~weidu_external/iwdification/languages~
    
    END    

  END

END

ASK_EVERY_COMPONENT

LANGUAGE ~English~ ~english~ 
  ~iwdification/languages/english/setup.tra~        // installer strings
  ~iwdification/languages/english/game_strings.tra~ // strings for the tlk

LANGUAGE ~French (by mickabouille)~ ~french~ 
  ~iwdification/languages/english/setup.tra~        // installer strings
  ~iwdification/languages/french/setup.tra~         // installer strings
  ~iwdification/languages/english/game_strings.tra~ // strings for the tlk
  ~iwdification/languages/french/game_strings.tra~  // strings for the tlk
                                            
LANGUAGE ~Spanish (by Gorlod)~ ~spanish~ 
  ~iwdification/languages/english/setup.tra~        // installer strings
  ~iwdification/languages/spanish/setup.tra~        // installer strings
  ~iwdification/languages/english/game_strings.tra~ // strings for the tlk
  ~iwdification/languages/spanish/game_strings.tra~ // strings for the tlk

LANGUAGE ~Russian (by Arkie)~ ~russian~ 
  ~iwdification/languages/english/setup.tra~        // installer strings
  ~iwdification/languages/russian/setup.tra~        // installer strings
  ~iwdification/languages/english/game_strings.tra~ // strings for the tlk
  ~iwdification/languages/russian/game_strings.tra~ // strings for the tlk

LANGUAGE ~Simplified Chinese~ ~schinese~ 
  ~iwdification/languages/english/setup.tra~         // installer strings
  ~iwdification/languages/schinese/setup.tra~        // installer strings
  ~iwdification/languages/english/game_strings.tra~  // strings for the tlk
  ~iwdification/languages/schinese/game_strings.tra~ // strings for the tlk

LANGUAGE ~Polish (by Patricida)~ ~polish~ 
  ~iwdification/languages/english/setup.tra~        // installer strings
  ~iwdification/languages/polish/setup.tra~         // installer strings
  ~iwdification/languages/english/game_strings.tra~ // strings for the tlk
  ~iwdification/languages/polish/game_strings.tra~  // strings for the tlk

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Icewind Dale Casting Graphics                    \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @1000 DESIGNATED 10
GROUP @994
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~70~ @998
LABEL ~cd_tweaks_iwd_casting_graphics~

INCLUDE ~iwdification/tpa/comp_70.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Commoners use drab colors                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @2000 DESIGNATED 20
GROUP @994
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~100~ @998
LABEL ~cd_tweaks_drab_commoners~

INCLUDE ~iwdification/tpa/comp_100.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// add two-handed axes                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @6000 DESIGNATED 60
GROUP @994
LABEL ~cd_iwdification_2h_axes~

COPY ~iwdification/bam/cdax2h1i.bam~ ~override~
     ~iwdification/bam/cdax2h2i.bam~ ~override~
     ~iwdification/bam/cdax2h3i.bam~ ~override~
     ~iwdification/bam/cdax2h4i.bam~ ~override~
     ~iwdification/bam/cdax2h5i.bam~ ~override~
     ~iwdification/bam/cdax2h5s.bam~ ~override~
     ~iwdification/bam/cdax2h6i.bam~ ~override~
     ~iwdification/spl/cdax2h5b.spl~ ~override~

COPY ~iwdification/itm/cdax2h1.itm~ ~override~ // generic 2h axe
  SAY 0x08 @6001
  SAY 0x0c @6001
  SAY 0x50 @6002
  SAY 0x54 @6002

COPY ~iwdification/itm/cdax2h2.itm~ ~override~ // +1 axe
  SAY 0x08 @6001
  SAY 0x0c @6003
  SAY 0x50 @6002
  SAY 0x54 @6004

COPY ~iwdification/itm/cdax2h3.itm~ ~override~ // +2 axe
  SAY 0x08 @6001
  SAY 0x0c @6005
  SAY 0x50 @6002
  SAY 0x54 @6006

COPY ~iwdification/itm/cdax2h4.itm~ ~override~ // +3 axe
  SAY 0x08 @6001
  SAY 0x0c @6007
  SAY 0x50 @6002
  SAY 0x54 @6008

COPY ~iwdification/itm/cdax2h5.itm~ ~override~ // +4 axe
  SAY 0x08 @6001
  SAY 0x0c @6009
  SAY 0x50 @6002
  SAY 0x54 @6010

COPY ~iwdification/itm/cdax2h6.itm~ ~override~ // +5 axe
  SAY 0x08 @6001
  SAY 0x0c @6011
  SAY 0x50 @6002
  SAY 0x54 @6012

COPY ~iwdification/spl/cdax2h5g.spl~ ~override~
  SAY 0x08 @6013
  SAY 0x0c @6013
  SAY 0x50 @6013
  SAY 0x54 @6013
  READ_LONG 0x08 string1

COPY ~iwdification/spl/cdax2h6g.spl~ ~override~
  SAY 0x08 @6014
  SAY 0x0c @6014
  SAY 0x50 @6014
  SAY 0x54 @6014
  READ_LONG 0x08 string2

APPEND ~tooltip.2da~ ~cdax2h5 15529 %string1% -1
cdax2h6 15529 %string2% -1~

COPY_EXISTING ~tooltip.2da~ ~override~
  PRETTY_PRINT_2DA
  
ACTION_IF enhanced_edition BEGIN

  // make 2h axes available to shamans
  COPY_EXISTING ~cdax2h1.itm~ ~override~
                ~cdax2h2.itm~ ~override~
                ~cdax2h3.itm~ ~override~
                ~cdax2h4.itm~ ~override~
                ~cdax2h5.itm~ ~override~
                ~cdax2h6.itm~ ~override~
    WRITE_BYTE 0x21 (THIS BAND `BIT6) // removes druid restriction, add back as 319 below
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 319 target = 1 parameter1 = 11 parameter2 = 5 timing = 2 special = 1080 END
    BUT_ONLY

END

/////                                                  \\\\\
///// if bg, add break code to +0 axe                  \\\\\
/////                                                  \\\\\

ACTION_IF GAME_IS ~bgee eet bgt tutu tutu_totsc~ BEGIN 

  ACTION_IF GAME_IS bgt BEGIN 
  
    COPY_EXISTING ~cdax2h1.itm~ ~override~
      LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 309 target = 1 parameter1 = 1 timing = 1 probability1 = 1 STR_VAR resource = cdax2h1 END

    COPY_EXISTING ~dplayer2.bcs~ ~override~
                  ~dplayer3.bcs~ ~override~
      DECOMPILE_AND_PATCH BEGIN
        APPEND_FILE ~iwdification/baf/bgt_breakage.baf~ // taken from tweaks anthology
        REPLACE_TEXTUALLY ~CD_REPLACE_ME~ ~cdax2h1~
      END
      BUT_ONLY
  
  END ELSE BEGIN // tutu, eet, bgee
      
    ACTION_IF enhanced_edition BEGIN // ees have quality versions that don't break
    
      COPY_EXISTING ~bprng1.itm~ ~override~ // black pits slave ring
        LPF CLONE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = sw1h01 resource = cdax2h1 END // ax1h01 missing by default, use sw1h01

      COPY_EXISTING ~cdax2h1.itm~ ~override/cdax2h1q.itm~ // make quality version before adding breakage code, below
        SAY 0x08 @6018 // Quality Two-Handed Axe
        SAY 0x0c @6018 // Quality Two-Handed Axe
      
      COPY_EXISTING ~baldur.bcs~ ~override~ // when taerom adds quality axes, also add 2h quality axes
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY ~\(AddStoreItem("taerum","ax1h01q",\)\([0-9]+,[0-9]+)\)~ ~\1\2 AddStoreItem("taerum","cdax2h1q",\2~
        END
        BUT_ONLY
        
    END     

    COPY_EXISTING ~cdax2h1.itm~ ~override~
      LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 146 target = 1 parameter2 = 1 timing = 1 probability1 = 0 STR_VAR resource = cdax2h1 END
      
    COPY_EXISTING ~%tutu_var%ax1h01.spl~  ~override/cdax2h1.spl~ 
      LPF ALTER_EFFECT STR_VAR match_resource = EVAL ~%SOURCE_RES%~ resource = cdax2h1 END
    
  END

END   

/////                                                  \\\\\
///// places axes in game                              \\\\\
/////                                                  \\\\\

// add generic 2h axes to any store that sells generic 1h axes; same with +1 models
COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x34 sale_off ELSE 0
  READ_LONG 0x38 sale_num ELSE 0
  SET ax_idx = 0
  SET ax1_idx = 0
  SET delta = 0
  FOR (index = 0 ; index < sale_num ; ++index) BEGIN
    READ_ASCII (sale_off +        (index * 0x1c)) item
    PATCH_IF (("%item%" STRING_COMPARE_CASE "ax1h01" = 0) OR ("%item%" STRING_COMPARE_CASE "_ax1h01" = 0)) BEGIN // 1h axe
      READ_ASCII (sale_off +        (index * 0x1c)) clone (28) // read entry
      SET ax_idx = (index + 1) // sets right after as insert point
    END ELSE
    PATCH_IF (("%item%" STRING_COMPARE_CASE "ax1h02" = 0) OR ("%item%" STRING_COMPARE_CASE "_ax1h02" = 0)) BEGIN // 1h axe +1
      READ_ASCII (sale_off +        (index * 0x1c)) clone1 (28) // read entry
      SET ax1_idx = (index + 1) // sets right after as insert point
    END
  END
  PATCH_IF (ax1_idx > 0) BEGIN // if 1h axes present, add in 2h axes
    INSERT_BYTES   (sale_off + (ax1_idx * 0x1c)) 28
      WRITE_ASCIIE (sale_off + (ax1_idx * 0x1c)) ~%clone1%~    // clones 1h axe +1 entry
      WRITE_ASCII  (sale_off + (ax1_idx * 0x1c)) ~cdax2h2~ #8 // change to 2h axe +1
    SET delta += 1
  END
  PATCH_IF (ax_idx > 0) BEGIN // if 1h axes present, add in 2h axes
    INSERT_BYTES   (sale_off + (ax_idx * 0x1c)) 28
      WRITE_ASCIIE (sale_off + (ax_idx * 0x1c)) ~%clone%~    // clones 1h axe entry
      WRITE_ASCII  (sale_off + (ax_idx * 0x1c)) ~cdax2h1~ #8 // change to 2h axe
    SET delta += 1
  END
  PATCH_IF (delta > 0) BEGIN
    WRITE_LONG 0x38 (sale_num + delta)
    PATCH_FOR_EACH offset IN 0x2c 0x4c 0x70 BEGIN
      READ_LONG offset off
      PATCH_IF (off > sale_off) BEGIN
        WRITE_LONG offset (THIS + (delta * 0x1c))
      END
    END
  END
  BUT_ONLY

ACTION_IF ((FILE_EXISTS_IN_GAME ~garkla.cre~) OR (FILE_EXISTS_IN_GAME ~_garkla.cre~)) THEN BEGIN // bgt/bgee/tutu

  COPY_EXISTING_REGEXP GLOB ~_?garkla.cre~ ~override~ // garclax at the bandit camp
    REPLACE_CRE_ITEM ~cdax2h3~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP TWOHANDED // add +2 axe to garclax in bandit camp

END

ACTION_IF ((FILE_EXISTS_IN_GAME ~ar0602.bcs~) AND (FILE_EXISTS_IN_GAME ~hlolaf.cre~)) THEN BEGIN // soa

  EXTEND_BOTTOM ~ar0602.bcs~ ~iwdification/baf/ar0602.baf~ // add generic 2H axe to CI

  COPY_EXISTING ~hlolaf.cre~ ~override~
    REPLACE_CRE_ITEM ~cdax2h3~ #0 #0 #0 ~NONE~ ~WEAPON1~ EQUIP TWOHANDED // add +2 axe to olaf at guarded compound
    IF_EXISTS

END

ACTION_IF FILE_EXISTS_IN_GAME ~botsmith.bcs~ THEN BEGIN // tob

  EXTEND_BOTTOM ~ar3017.bcs~ ~iwdification/baf/ar3017.baf~ // add generic 2H axe to wk (pile of normal weapons for magic golems)

  EXTEND_BOTTOM ~botsmith.bcs~ ~iwdification/baf/botsmith.baf~ // cespy's upgrade of the +4 axe to +5
  COMPILE ~iwdification/dlg/botsmith.d~

  // add +3 2h axes to any store that sells generic +3 1h axes (opening stores in ToB)
  COPY_EXISTING ~amsmug01.sto~ ~override~
                ~amsmug02.sto~ ~override~
    ADD_STORE_ITEM ~cdax2h4~ AFTER ~ax1h17~ #0 #0 #0 ~IDENTIFIED~ #1 // +3 2h axe

  // add +3 2h axes to any store that sells generic +3 1h axes (opening stores in ToB)
  COPY_EXISTING ~sarbar01.sto~ ~override~
    ADD_STORE_ITEM ~cdax2h4~ AFTER ~ax1h17~ #0 #0 #0 ~IDENTIFIED~ #3 // +3 2h axe
  
  COPY_EXISTING ~gromg04.cre~ ~override~
    REPLACE_CRE_ITEM ~cdax2h5~ #2 #2 #2 ~NONE~ ~WEAPON1~ EQUIP TWOHANDED // add +4 axe to one of gromnir's bodyguards

END

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// expanded polymorph self                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @9000 DESIGNATED 90
GROUP @994  
LABEL ~cd_iwdification_expanded_polymorph_self~

// first, some general cleanup and fixes to the spells
COPY_EXISTING ~spwi498.spl~ ~override~ // Black Bear
  LPF ALTER_EFFECT INT_VAR match_opcode = 111 STR_VAR resource = PLYBEAR2 END
  
// create different weapons for black/brown bears  
COPY_EXISTING ~plybear1.itm~ ~override/plybear2.itm~ // Black Bear
  
ACTION_CLEAR_ARRAY cd_poly_self
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_poly_self BEGIN

  // item, creature => spell
  plyflind, plyflind => spwi493 // flind
  plymstar, plyogre  => spwi494 // ogre
  plyspid,  plyspid  => spwi495 // spider
  plyjelly, jellmu   => spwi496 // mustard jelly
  plybear1, bearbr   => spwi497 // brown bear
  plybear2, bearbl   => spwi498 // black bear
  plywolf1, plywolf  => spwi499 // wolf
  
END

ACTION_PHP_EACH cd_poly_self AS params => spell BEGIN  

  // first, some general cleanup and fixes to the spells
  COPY_EXISTING ~%spell%.spl~ ~override~
    LPF DELETE_EFFECT INT_VAR match_opcode = 112 END // remove 'remove item' calls
    LPF DELETE_EFFECT INT_VAR match_opcode = 135 END // move polymorph to claws
    LPF DELETE_EFFECT INT_VAR match_opcode = 144 END // move disable button to claws, too
    LPF DELETE_EFFECT INT_VAR match_opcode = 172 END // get a clean start for the spell removals
    LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 172 target = 1 timing = 1 STR_VAR resource = spin974 END // remove spell
    PATCH_FOR_EACH res IN spin122 spin123 spin124 spin150 spin151 spin160 spin160 spin160 spin529 spin667 spin718 spinhum spmdslay spwi489 spwi490 spwi491 BEGIN
      LPF CLONE_EFFECT INT_VAR match_opcode = 172 STR_VAR match_resource = spin974 resource = EVAL ~%res%~ END
    END
    LPF CLONE_EFFECT INT_VAR match_opcode = 171 STR_VAR match_resource = EVAL ~%spell%~ resource = spwi490 END
    BUT_ONLY    

  COPY_EXISTING ~%params_0%.itm~ ~override~
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 144 target = 1 parameter2 = 7 timing = 2 END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 135 target = 1 timing = 2 STR_VAR resource = EVAL ~%params_1%~ END  
    BUT_ONLY      

END

OUTER_SET anim_beetle = 0 // 30720 gibb for testing 
ACTION_IF enhanced_edition BEGIN 

  OUTER_SET anim_beetle = 57888
  COPY ~iwdification/bam/e220.ini~                         ~override~
       ~iwdification/iwdspells/copyover/anim/beetle_black~ ~override~
       
END ELSE BEGIN

  ACTION_IF MOD_IS_INSTALLED ~INFINITYANIMATIONS/SETUP-INFINITYANIMATIONS.TP2~ ~500~ BEGIN // 'more base animations'

    ACTION_IF IDS_OF_SYMBOL (~animate~ ~BEETLE_BLACK~) > 0 THEN BEGIN
      OUTER_SET anim_beetle = IDS_OF_SYMBOL (~animate~ ~BEETLE_BLACK~)
    END
  
  END

END  

COPY ~iwdification/bam/cdia480.bam~  ~override~ // bb weapon icon
     ~iwdification/bam/cdia480b.bam~ ~override~ // bb spell icon
     ~iwdification/bam/cdia481.bam~  ~override~ // pb weapon icon
     ~iwdification/bam/cdia481b.bam~ ~override~ // pb spell icon
     ~iwdification/bam/cdia482.bam~  ~override~ // ww weapon icon
     ~iwdification/bam/cdia482b.bam~ ~override~ // ww spell icon
     ~iwdification/cre/cdia481.cre~  ~override~ // polar bear creature doesn't need strings
     ~iwdification/cre/cdia482.cre~  ~override~ // winter wolf creature doesn't need strings

COPY ~iwdification/cre/cdia480.cre~ ~override~ // boring beetle creature does need string, animation
  SAY 0x08 @9001
  SAY 0x0c @9001
  WRITE_LONG 0x28 anim_beetle

COPY ~iwdification/itm/cdia480.itm~  ~override~ // bb weapon
     ~iwdification/itm/cdia481.itm~  ~override~ // pb weapon
     ~iwdification/itm/cdia482.itm~  ~override~ // ww weapon
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 144 target = 1 parameter2 = 7 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 135 target = 1 timing = 2 STR_VAR resource = EVAL ~%SOURCE_RES%~ END

// these generally 'clean out' the poly self subspells
COPY_EXISTING ~#rdremov.spl~ ~override~ // resurrection subspell (2.6)
              ~bhaal4a.spl~  ~override~ // resurrection
              ~ohbraise.spl~ ~override~ // nsi
              ~slayerw3.itm~ ~override~ // slayer weapon
              ~slayerw2.spl~ ~override~ // slayer transformation (Ascension)
              ~slayerw3.spl~ ~override~ // slayer transformation (Ascension)
              ~slayerw4.spl~ ~override~ // slayer transformation (Ascension)
              ~spin823.spl~  ~override~ // slayer change
              ~spja01.spl~   ~override~ // harper's call
              ~sppr504.spl~  ~override~ // raise dead
              ~sppr550.spl~  ~override~ // recall spirit
              ~sppr712.spl~  ~override~ // resurrection
              ~sppr729.spl~  ~override~ // mass raise dead
              ~spwish10.spl~ ~override~ // mass raise dead
  LPF CLONE_EFFECT INT_VAR silent = 1 STR_VAR match_resource = spwi499 resource = cdia480 END // boring beetle
  LPF CLONE_EFFECT INT_VAR silent = 1 STR_VAR match_resource = spwi499 resource = cdia481 END // polar bear
  LPF CLONE_EFFECT INT_VAR silent = 1 STR_VAR match_resource = spwi499 resource = cdia482 END // winter wolf
  BUT_ONLY IF_EXISTS

// add new forms to poly self
COPY_EXISTING ~spwi416.spl~  ~override~ // poly self
  PATCH_IF anim_beetle BEGIN
    LPF CLONE_EFFECT STR_VAR silent = 1 match_resource = spwi499 resource = cdia480 END // boring beetle
    LPF CLONE_EFFECT STR_VAR silent = 1 match_resource = spwi461 resource = cdia480 END // boring beetle (SR)
    SAY 0x50 @9004
  END ELSE BEGIN
    SAY 0x50 @9002
  END
  LPF CLONE_EFFECT STR_VAR silent = 1 match_resource = spwi499 resource = cdia481 END // polar bear
  LPF CLONE_EFFECT STR_VAR silent = 1 match_resource = spwi499 resource = cdia482 END // winter wolf
  LPF CLONE_EFFECT STR_VAR silent = 1 match_resource = spwi461 resource = cdia481 END // polar bear (SR)
  LPF CLONE_EFFECT STR_VAR silent = 1 match_resource = spwi461 resource = cdia482 END // winter wolf (SR)

COPY_EXISTING ~spwi499.spl~ ~override/cdia480.spl~ // shapeshift: boring beetle
              ~spwi499.spl~ ~override/cdia481.spl~ // shapeshift: polar bear
              ~spwi499.spl~ ~override/cdia482.spl~ // shapeshift: winter wolf
  SAY 0x08 @9005
  SAY 0x50 @9006
  WRITE_ASCIIE 0x3a ~%DEST_RES%b~ #8
  LPF ALTER_SPELL_HEADER STR_VAR icon = EVAL ~%DEST_RES%b~ END
  LPF ALTER_EFFECT INT_VAR match_opcode = 111 STR_VAR resource = EVAL ~%DEST_RES%~ END // give weapon
  LPF ALTER_EFFECT INT_VAR match_opcode = 171 STR_VAR match_resource = spwi499 resource = EVAL ~%DEST_RES%~ END // give innate

COPY_EXISTING ~cdia481.spl~ ~override~ // shapeshift: polar bear
  SAY 0x08 @9007
  SAY 0x50 @9008

COPY_EXISTING ~cdia482.spl~ ~override~ // shapeshift: winter wolf
  SAY 0x08 @9009
  SAY 0x50 @9010

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// Use IWD Damage Animations                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @13000 DESIGNATED 130
GROUP @994
REQUIRE_PREDICATE !GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @997
LABEL ~cd_iwdification_damage_animations~

COPY_EXISTING ~dmgtypes.2da~ ~override~
  REPLACE_TEXTUALLY ~^\(MAGICFIRE\|FIRE\)[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+~ ~\1        MISC_01C   FIREH      FIREL~
  REPLACE_TEXTUALLY ~^\(MAGICCOLD\|COLD\)[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+~ ~\1        EFF_M46    COLDH      COLDL~
  REPLACE_TEXTUALLY ~^ELECTRICITY[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+~ ~ELECTRICITY MISC_02C   ELECTRH    ELECTRL~
  REPLACE_TEXTUALLY ~^ACID[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+[ %TAB%]+[^ %TAB%]+~        ~ACID        EFF_M47    ACIDH      *~
  PRETTY_PRINT_2DA
  
COPY ~iwdification/bam/blank.bam~                   ~override/shacid.bam~
     ~iwdification/bam/coldh.bam~                   ~override~
     ~iwdification/bam/coldl.bam~                   ~override~
     ~iwdification/bam/electrh.bam~                 ~override~
     ~iwdification/bam/electrl.bam~                 ~override~
     ~iwdification/bam/fireh.bam~                   ~override~
     ~iwdification/bam/firel.bam~                   ~override~
     ~iwdification/iwdspells/obg2_res/acidh.vvc~    ~override~
     ~iwdification/iwdspells/obg2_res/acidh_ee.bam~ ~override/acidh.bam~
     ~iwdification/vvc/coldh.vvc~                   ~override~
     ~iwdification/vvc/electrh.vvc~                 ~override~
     ~iwdification/vvc/fireh.vvc~                   ~override~
     ~iwdification/wav/eff_m46.wav~                 ~override~
     ~iwdification/wav/eff_m47.wav~                 ~override~
     ~iwdification/wav/misc_01c.wav~                ~override~
     ~iwdification/wav/misc_02c.wav~                ~override~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// randomized equipment                             \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// include hq items                                 \\\\\
/////                                                  \\\\\

BEGIN @14001 DESIGNATED 140 // with hq items
SUBCOMPONENT @14000 // randomized equipment
GROUP @994  
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @997
LABEL ~cd_iwdification_randomized_equipment_hq~

OUTER_SET include_hq = 1
INCLUDE ~iwdification/tpa/random.tpa~

/////                                                  \\\\\
///// exclude hq items                                 \\\\\
/////                                                  \\\\\

BEGIN @14002 DESIGNATED 141 // with hq items
SUBCOMPONENT @14000 // randomized equipment
GROUP @994  
REQUIRE_PREDICATE GAME_IS ~bgee bg2ee eet~ @997
LABEL ~cd_iwdification_randomized_equipment~

OUTER_SET include_hq = 0
INCLUDE ~iwdification/tpa/random.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// base spear damage                                \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @19000 DESIGNATED 190
GROUP @994  
LABEL ~cd_iwdification_spear_damage~

OUTER_SPRINT damage_match @19001

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x1c itype
  PATCH_IF itype = 29 BEGIN // spear
    READ_LONG  0x64 abil_off
    READ_SHORT 0x68 abil_num
    SET altered = 0
    FOR (index = 0 ; index < abil_num ; ++index) BEGIN
      READ_BYTE (abil_off + 0x00 + (index * 0x38)) type
      PATCH_IF ((type = 1) OR (type = 2)) BEGIN // melee or ranged header
        READ_BYTE (abil_off + 0x16 + (index * 0x38)) dicesize
        PATCH_IF dicesize BEGIN // don't update if no damage
          SET altered = 1
          WRITE_BYTE (abil_off + 0x16 + (index * 0x38)) THIS + 2
        END
      END
    END      
    PATCH_IF altered BEGIN // update descript if altered
      PATCH_FOR_EACH offset IN 0x50 0x54 BEGIN
        READ_STRREF ~%offset%~ desc
        PATCH_IF ("%desc%" STRING_COMPARE_REGEXP ~<Invalid Strref -?[0-9]+>~) BEGIN // checking for valid content in string
          INNER_PATCH_SAVE desc ~%desc%~ BEGIN
            REPLACE_EVALUATE ~\(%damage_match%\)\([0-9]+\)~ BEGIN 
              SET MATCH2 += 2
            END ~%MATCH1%%MATCH2%~
          END
          SAY_EVALUATED ~%offset%~ ~%desc%~ // write changes
        END
      END
    END
  END
  BUT_ONLY   
        
  
  
  
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// IWD Class Updates: Bard                          \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// bard songs                                       \\\\\
/////                                                  \\\\\ 

BEGIN @5000 DESIGNATED 50 // aka add new bard songs
REQUIRE_PREDICATE FILE_EXISTS ~iwdification/iwdspells/lang/%LANGUAGE%/dw_iwdspells.tra~ @992
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~stratagems/setup-stratagems.tp2~ 1520 @998
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~dw_som/dw_som.tp2~ 1520 @998
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~dw_talents/dw_talents.tp2~ 1520 @998
GROUP @995
LABEL ~cd_iwdification_bard_songs~
// When using iwdification, ToTLM and rr there is a circular dependency because ToTLM expects IWDifdicatiion
// ahead of it for the IWD spells but expects RR after it, while IWDification expects RR before due to Bard Songs.
// Instead of setting a global install order rule for all iwdification components (iwdification.ini) define it only for a single component.
// Remove when rr will be updated to be compatible with iwdification so the install order won't matter.
METADATA ~AFTER = rr~

INCLUDE "%MOD_FOLDER%/iwdspells/iwdspells_extern.tpa"
LAF iwdspells_extern STR_VAR type=bard 
                             iconv_path="languages/iconv" // if you use these values
                             ini_name="dw_iwdspells.ini"
                             oggdec_path="wav"
                             sox_path="wav"END

/////                                                  \\\\\
///// switchable skald/jester/EBS bard songs           \\\\\
/////                                                  \\\\\ 

BEGIN @18000 DESIGNATED 180 // allow skald/jester to switch between their songs and EBS
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @991                 // requires tob
REQUIRE_PREDICATE !FILE_EXISTS_IN_GAME ~rr#bdf01.spl~ @991 // skip if RR bard song HLAs installed
GROUP @995
LABEL ~cd_iwdification_bard_kit_selectable_songs~

// why not blade? unlike jesters and skalds, EBS is better than their default bard 
// song so there would never be a reason to switch back. same reason this component
// is skipped with RR, since it has skald/jester bard song HLAs that are better

OUTER_SET skald_name  = RESOLVE_STR_REF (@18001)
OUTER_SET skald_desc  = RESOLVE_STR_REF (@18002)
OUTER_SET jester_name = RESOLVE_STR_REF (@18003)
OUTER_SET jester_desc = RESOLVE_STR_REF (@18004)

COPY ~iwdification/bam/cdibsjes.bam~ ~override~
     ~iwdification/bam/cdibsska.bam~ ~override~

ACTION_CLEAR_ARRAY cd_selectable_bard_song
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_selectable_bard_song BEGIN

  // spell which changes bard song, spell of the actual new bard song, CLAB, BAM, name strref, desc strref => new ability for selecting bard song
  spcl751, spcl751a, clabba03, cdibsjes, ~%jester_name%~, ~%jester_desc%~ => cdcl751   // jester
  spcl542, spcl542a, clabba04, cdibsska, ~%skald_name%~,  ~%skald_desc%~  => cdcl542   // skald
  
END

INCLUDE ~iwdification/tpa/switchable_bard_song.tpa~

/////                                                  \\\\\
///// IWD Spell Progression                            \\\\\
/////                                                  \\\\\

BEGIN @15000 DESIGNATED 150 // bard spell table
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2270~ @998
GROUP @995
LABEL ~cd_tweaks_bard_spell_progression_unnerfed~

INCLUDE ~iwdification/tpa/comp_2270.tpa~
  
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// IWD Class Updates: Druid                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// druid shapeshifting & abilities                  \\\\\
/////                                                  \\\\\

BEGIN @7000 DESIGNATED 70 // Use IWD Shapeshifting and Ability Progression
GROUP @995
LABEL ~cd_iwdification_druid_abilities~

INCLUDE ~iwdification/tpa/druid_poly.tpa~

/////                                                  \\\\\
///// Elven Druids                                     \\\\\
/////                                                  \\\\\

BEGIN @7100 DESIGNATED 71
REQUIRE_PREDICATE FILE_EXISTS_IN_GAME ~clsrcreq.2da~ @997
GROUP @995
LABEL ~cd_iwdification_elven_druids~

INCLUDE ~iwdification/tpa/druid_elf.tpa~

/////                                                  \\\\\
///// do both                                          \\\\\
/////                                                  \\\\\

BEGIN ~deprecated~ DESIGNATED 72 DEPRECATED ~now separate, independent components~
  
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// IWD Class Updates: Paladin                       \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// Abilities and skills                             \\\\\
/////                                                  \\\\\

BEGIN @10000 DESIGNATED 100
GROUP @995
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~dw_talents/dw_talents.tp2~ 40200 @998
LABEL ~cd_iwdification_paladin_abilities~

LAF HANDLE_AUDIO STR_VAR audio_path = ~iwdification/wav/paladin~ oggdec_path = ~iwdification/wav~ END

// change paladin detect evil to at-will
COPY_EXISTING ~spcl212.spl~ ~override~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 171 target = 1 timing = 1 STR_VAR resource = spcl212 END
  LPF CLONE_EFFECT INT_VAR match_opcode = 171 opcode = 172 END

// change paladin protection from evil to caster only, 24-hr duration
COPY_EXISTING ~spcl213.spl~ ~override~
  LPF ALTER_EFFECT INT_VAR match_target = 2 target = 1 END
  LPF ALTER_SPELL_EFFECT INT_VAR duration_high = 7200 match_opcode = "-1" END
//  LPF ALTER_EFFECT INT_VAR match_duration = 60 duration = 7200 END
  LPF ALTER_SPELL_HEADER INT_VAR target = 5 END // caster
  
COPY ~iwdification/bam/cdiplseb.bam~ ~override~ // smite evil icon
     ~iwdification/bam/cdiplsec.bam~ ~override~ // smite evil icon
     ~iwdification/spl/cdiplid.spl~  ~override~ // disease immunity

OUTER_SET passive  = RESOLVE_STR_REF (@10009)  
COPY ~iwdification/spl/cdiplif.spl~ ~override~ // fear immunity
  LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = passive END
  
COPY ~iwdification/spl/cdiplse.spl~ ~override~ // smite evil
  SAY 0x08 @10001
  SAY 0x50 @10002
  WRITE_ASCII 0x3a cdiplsec
  LPF ALTER_SPELL_HEADER STR_VAR icon = cdiplseb END // location: ability
  
COPY_EXISTING ~sppr317.spl~ ~override/cdiplcd.spl~
  WRITE_SHORT 0x1c 4 // innate  
  WRITE_LONG  0x34 1 // level
  LPF ALTER_SPELL_HEADER INT_VAR location = 4 END // location: ability
  
ACTION_IF !enhanced_edition BEGIN
  
  COPY_EXISTING ~cdiplif.spl~ ~override~ // fear immunity
    LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
  COPY_EXISTING ~cdiplse.spl~ ~override~ // smite evil
    LPF DELETE_EFFECT INT_VAR match_opcode = 324 END
    FOR (index = 0 ; index < 7 ; ++index) BEGIN
      LPF ALTER_EFFECT INT_VAR header = index match_opcode = 12 opcode = 177 parameter1 = 3 parameter2 = 8 STR_VAR resource = EVAL ~cdiplse%index%~ END
    END 
    
  OUTER_FOR (index = 0 ; index < 7 ; ++index) BEGIN
  
    COPY ~iwdification/eff/cdiplse0.eff~ ~override/cdiplse%index%.eff~
      WRITE_LONG 0x38 (index + 1) // number of dice
      
  END

END

OUTER_SET default_paladin = 9558
ACTION_CLEAR_ARRAY  cd_pal_clabs
ACTION_DEFINE_ASSOCIATIVE_ARRAY cd_pal_clabs BEGIN // start with base class tables
  ~%default_paladin%~, clabpa01, 0 => 1 // generic paladin abilities
END
  
// add kit tables dynamically by reading kitlist
COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_ROWS 9 rows
  FOR (index = 2 ; index < rows ; ++index) BEGIN // skip reserve row
    READ_2DA_ENTRY index 8 9 class
    PATCH_IF class = 6 BEGIN
      READ_2DA_ENTRY index 1 9 kitid
      READ_2DA_ENTRY index 4 9 desc
      READ_2DA_ENTRY index 5 9 clab
      DEFINE_ASSOCIATIVE_ARRAY cd_pal_clabs BEGIN "%desc%", "%clab%", "%kitid%" => 0 END
    END  
  END
  BUT_ONLY
  
ACTION_PHP_EACH cd_pal_clabs AS params => update BEGIN

  ACTION_IF (RESOURCE_CONTAINS ~%params_1%.2da~ ~[ %TAB%]GA_SPCL212[ %TAB%]~) BEGIN   // returns false if non-existant or doesn't contain text
  
    COPY_EXISTING ~kit.ids~ ~override~
      REPLACE_EVALUATE ~^\([^ %TAB%]+\)\([ %TAB%]+%params_2%[ %TAB%%LNL%%MNL%%WNL%]\)~ BEGIN  
        SET new_kitid = ~%MATCH1%~
        DEFINE_ASSOCIATIVE_ARRAY cd_pal_clabs BEGIN "%params_0%", "%params_1%", "%new_kitid%" => 1 END // mark as active
      END ~%MATCH1%%MATCH2%~  
      BUT_ONLY 
            
    ACTION_IF (!RESOURCE_CONTAINS ~%params_1%.2da~ ~[ %TAB%]GA_cdiplse[ %TAB%]~) BEGIN // make sure we're not re-patching a file
            
      COPY_EXISTING ~%params_1%.2da~ ~override~
        REPLACE_TEXTUALLY ~\([ %TAB%]\)GA_SPCL212\([ %TAB%%LNL%%MNL%%WNL%]\)~ ~\1****\2~
        REPLACE_TEXTUALLY ~\([ %TAB%]\)GA_SPCL213\([ %TAB%%LNL%%MNL%%WNL%]\)~ ~\1****\2~

      APPEND ~%params_1%.2da~ ~ABILITY    AP_cdiplid  ****        AP_cdiplif  CDREPLACE
ABILITY    GA_SPCL212  ****        ****        CDREPLACE
ABILITY    GA_SPCL213  ****        ****        CDREPLACE
ABILITY    GA_cdiplse  ****        ****        CDREPLACE
ABILITY    GA_cdiplcd  ****        ****        CDREPLACE~
    
      COPY_EXISTING ~%params_1%.2da~ ~override~
        COUNT_2DA_COLS cols
        FOR (index = 5 ; index < cols ; ++index) BEGIN
          REPLACE_TEXTUALLY ~CDREPLACE~ ~**** CDREPLACE~
        END
        REPLACE_TEXTUALLY ~CDREPLACE~ ~****~
        PRETTY_PRINT_2DA
        BUT_ONLY
        
    END    
 
    ACTION_GET_STRREF params_0 desc
    OUTER_PATCH_SAVE desc "%desc%" BEGIN
      SPRINT old @10003
      SPRINT new @10006
      REPLACE_TEXTUALLY ~%new%~ ~~ // purge in case the text is already there
      REPLACE_TEXTUALLY ~[%LNL%%MNL%%WNL%]*\([%LNL%%MNL%%WNL%]\)\(%old%\)~ ~\1%new%~ // swap text
      SPRINT old @10004
      SPRINT new @10007
      REPLACE_TEXTUALLY ~%new%~ ~~ // purge in case the text is already there
      REPLACE_TEXTUALLY ~[%LNL%%MNL%%WNL%]*\([%LNL%%MNL%%WNL%]\)\(%old%\)~ ~\1%new%~ // swap text
      SPRINT old @10005
      SPRINT new @10008
      REPLACE_TEXTUALLY ~%new%~ ~~ // purge in case the text is already there
      REPLACE_TEXTUALLY ~[%LNL%%MNL%%WNL%]*\([%LNL%%MNL%%WNL%]\)\(%old%\)~ ~\1%new%~ // swap text
    END
    STRING_SET_EVALUATE params_0 "%desc%"  

  END

END
  
// handing out some abilities
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  SET paladin_level = 0
  READ_BYTE 0x273 class  // V1.0
  PATCH_IF class = 6 BEGIN
    READ_SHORT 0x246 kit
    PATCH_IF (kit = 0) OR (kit = 0x4000) BEGIN // easy if unkitted
      READ_BYTE 0x234 paladin_level
    END ELSE BEGIN  
      PATCH_PHP_EACH cd_pal_clabs AS params => update BEGIN // work through paladin kits
        PATCH_IF update BEGIN // only if the clab got updated
          PATCH_IF (kit = params_2) BEGIN // if matched and got an updated clab
            READ_BYTE 0x234 paladin_level
          END
        END  
      END
    END
  END   
  PATCH_IF paladin_level BEGIN // if any paladin level
    REMOVE_MEMORIZED_SPELL ~spcl212~ ~spcl213~ // remove all detect evil & protection from evil
    ADD_MEMORIZED_SPELL spcl212 #0 ~innate~ (1) // replace with one casting
    ADD_MEMORIZED_SPELL spcl213 #0 ~innate~ (1) // replace with one casting
    ADD_KNOWN_SPELL     cdiplse #0 ~innate~
    ADD_KNOWN_SPELL     cdiplcd #0 ~innate~
    ADD_MEMORIZED_SPELL cdiplse #0 ~innate~ (1) // add smite evil
    ADD_MEMORIZED_SPELL cdiplcd #0 ~innate~ (1) // add cure disease
    LPF ADD_CRE_EFFECT INT_VAR opcode = 101 target = 1 timing = 1 parameter2 = 78 STR_VAR effsource = cdiplid END // immunity to disease
    LPF ADD_CRE_EFFECT INT_VAR opcode = 267 target = 1 timing = 1 parameter1 = 54337 STR_VAR effsource = cdiplid END // string immunity: diseased
    LPF ADD_CRE_EFFECT INT_VAR opcode = 267 target = 1 timing = 1 parameter1 = 39752 STR_VAR effsource = cdiplid END // string immunity: Stricken by a foul disease
    PATCH_IF paladin_level > 2 BEGIN // add fear immunity, too
      PATCH_IF enhanced_edition BEGIN
        LPF ADD_CRE_EFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = spwi205 effsource = cdiplif END // remove spell
        LPF ADD_CRE_EFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = spin105 effsource = cdiplif END // remove spell
      END 
      LPF ADD_CRE_EFFECT INT_VAR opcode = 296 target = 1 timing = 1 parameter2 = 106 STR_VAR resource = cdhorror effsource = cdiplif END // immunity to animation
      LPF ADD_CRE_EFFECT INT_VAR opcode = 267 target = 1 timing = 1 parameter1 = 14007 STR_VAR effsource = cdiplif END // string immunity: panic
      LPF ADD_CRE_EFFECT INT_VAR opcode = 267 target = 1 timing = 1 parameter1 = 17427 STR_VAR effsource = cdiplif END // string immunity: panic
      LPF ADD_CRE_EFFECT INT_VAR opcode = 267 target = 1 timing = 1 parameter1 = 20568 STR_VAR effsource = cdiplif END // string immunity: morale failure: panic
      LPF ADD_CRE_EFFECT INT_VAR opcode = 161 target = 1 timing = 1 STR_VAR effsource = cdiplif END // remove fear
      LPF ADD_CRE_EFFECT INT_VAR opcode =  23 target = 1 timing = 1 STR_VAR effsource = cdiplif END // reset morale
      LPF ADD_CRE_EFFECT INT_VAR opcode = 106 target = 1 timing = 1 parameter2 = 1 STR_VAR effsource = cdiplif END // morale break set
      LPF ADD_CRE_EFFECT INT_VAR opcode = 101 target = 1 timing = 1 parameter2 = 23 insert_point = 999 STR_VAR effsource = cdiplif END // immunity to reset morale
      LPF ADD_CRE_EFFECT INT_VAR opcode = 101 target = 1 timing = 1 parameter2 = 106 insert_point = 999 STR_VAR effsource = cdiplif END // immunity to morale break
      LPF ADD_CRE_EFFECT INT_VAR opcode = 101 target = 1 timing = 1 parameter2 = 24 insert_point = 999 STR_VAR effsource = cdiplif END // immunity to panic
    END // end fear immunity
  END // other paladin stuff
  BUT_ONLY  

/////                                                  \\\\\
///// IWD Spell Progression                            \\\\\
/////                                                  \\\\\

BEGIN @16000 DESIGNATED 160
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2390~ @998
GROUP @995
LABEL ~cd_tweaks_paladin_spell_progression_iwd~

INCLUDE ~iwdification/tpa/comp_2390.tpa~

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// IWD Class Updates: Ranger                        \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// IWD Class Updates: Ranger: Tracking for all      \\\\\
/////                                                  \\\\\

BEGIN @11001 DESIGNATED 110
SUBCOMPONENT @11000
//REQUIRE_PREDICATE GAME_IS ~bgee eet tutu tutu_totsc bgt~ @991                          // only bg content at the moment
REQUIRE_PREDICATE FILE_EXISTS ~iwdification/languages/%LANGUAGE%/tracking_bg.tra~ @992 // language check; can't use tra_location yet since it may not have been set
GROUP @995

OUTER_SET tracking_kits = 1
INCLUDE ~iwdification/tpa/tracking.tpa~

/////                                                  \\\\\
///// IWD Class Updates: Ranger: Tracking for unkitted \\\\\
/////                                                  \\\\\

BEGIN @11100 DESIGNATED 111
SUBCOMPONENT @11000
//REQUIRE_PREDICATE GAME_IS ~bgee eet tutu tutu_totsc bgt~ @991                          // only bg content at the moment
REQUIRE_PREDICATE FILE_EXISTS ~iwdification/languages/%LANGUAGE%/tracking_bg.tra~ @992 // language check; can't use tra_location yet since it may not have been set
GROUP @995

OUTER_SET tracking_kits = 0
INCLUDE ~iwdification/tpa/tracking.tpa~

/////                                                  \\\\\
///// IWD Spell Progression                            \\\\\
/////                                                  \\\\\

BEGIN @17000 DESIGNATED 170
REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~cdtweaks/setup-cdtweaks.tp2~ ~2400~ @998
GROUP @995
LABEL ~cd_tweaks_ranger_spell_progression_iwd~

OUTER_SET game_is_iwd2 = 0
INCLUDE ~iwdification/tpa/comp_2400.tpa~
  
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// IWD Class Updates: Thief                         \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

/////                                                  \\\\\
///// evasion: include kits                            \\\\\
/////                                                  \\\\\

BEGIN @12005 DESIGNATED 120
SUBCOMPONENT @12000
REQUIRE_PREDICATE !GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @997
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~dw_talents/dw_talents.tp2~ 40450 @998
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~dw_talents/dw_talents.tp2~ 40460 @998
GROUP @995
LABEL ~cd_iwdification_thief_evasion~

OUTER_SET evasion_kits = 1 
INCLUDE ~iwdification/evasion/evasion_main.tpa~ 

/////                                                  \\\\\
///// evasion: exclude kits                            \\\\\
/////                                                  \\\\\

BEGIN @12100 DESIGNATED 121
SUBCOMPONENT @12000
REQUIRE_PREDICATE !GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @997
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~dw_talents/dw_talents.tp2~ 40450 @998
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~dw_talents/dw_talents.tp2~ 40460 @998
GROUP @995
LABEL ~cd_iwdification_thief_evasion_no_kits~

OUTER_SET evasion_kits = 0 
INCLUDE ~iwdification/evasion/evasion_main.tpa~ 

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// IWD arcane spell pack                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @3000 DESIGNATED 30
REQUIRE_PREDICATE FILE_EXISTS ~iwdification/iwdspells/lang/%LANGUAGE%/dw_iwdspells.tra~ @992
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~stratagems/setup-stratagems.tp2~ 1500 @998
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~dw_som/dw_som.tp2~ 1500 @998
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~dw_talents/dw_talents.tp2~ 1500 @998
GROUP @993
LABEL ~cd_iwdification_arcane_spells~

INCLUDE "%MOD_FOLDER%/iwdspells/iwdspells_extern.tpa"
LAF iwdspells_extern 
  STR_VAR type=arcane 
          custom_includes="iwdspells/custom/cd_scroll_placement.tpa iwdspells/custom/cd_arcane_spell_use.tpa" 
          iconv_path="languages/iconv" // if you use these values
          ini_name="dw_iwdspells.ini"
          oggdec_path="wav"
          sox_path="wav"
END 

/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// IWD divine spell pack                            \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @4000 DESIGNATED 40
REQUIRE_PREDICATE FILE_EXISTS ~iwdification/iwdspells/lang/%LANGUAGE%/dw_iwdspells.tra~ @992
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~stratagems/setup-stratagems.tp2~ 1510 @998
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~dw_som/dw_som.tp2~ 1510 @998
REQUIRE_PREDICATE !MOD_IS_INSTALLED ~dw_talents/dw_talents.tp2~ 1510 @998
GROUP @993
LABEL ~cd_iwdification_divine_spells~

INCLUDE "%MOD_FOLDER%/iwdspells/iwdspells_extern.tpa"
LAF iwdspells_extern 
  STR_VAR type=divine 
          custom_includes="iwdspells/custom/cd_divine_spell_use.tpa" 
          iconv_path="languages/iconv" // if you use these values
          ini_name="dw_iwdspells.ini"
          oggdec_path="wav"
          sox_path="wav"
END 
  
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////                                                  \\\\\
///// obg2 portrait icons                              \\\\\
/////                                                  \\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\
/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\/////\\\\\

BEGIN @8000 DESIGNATED 80
GROUP @993
REQUIRE_PREDICATE GAME_IS ~soa tob bgt ca tutu tutu_totsc iwd_in_bg2~ @996
// REQUIRE_PREDICATE NOT MOD_IS_INSTALLED ~stratagems/setup-stratagems.tp2~ ~1510~ @998 Future SCS?
LABEL ~cd_iwdification_obg2_portrait_icons~
  
INCLUDE "%MOD_FOLDER%/iwdspells/iwdspells_extern.tpa"
LAF iwdspells_extern STR_VAR type="icon" 
                             iconv_path="languages/iconv" // if you use these values
                             ini_name="dw_iwdspells.ini"
                             oggdec_path="wav"
                             sox_path="wav"END
  
